pragma solidity ^0.4.18;

/**
 * @title SafeMath
 * @dev Math operations with safety checks that throw on error
 */
library SafeMath {
  function mul(uint256 a, uint256 b) internal constant returns (uint256) {
    uint256 c = a * b;
    assert(a == 0 || c / a == b);
    return c;
  }

  function div(uint256 a, uint256 b) internal constant returns (uint256) {
    // assert(b > 0); // Solidity automatically throws when dividing by 0
    uint256 c = a / b;
    // assert(a == b * c + a % b); // There is no case in which this doesn't hold
    return c;
  }

  function sub(uint256 a, uint256 b) internal constant returns (uint256) {
    assert(b <= a);
    return a - b;
  }

  function add(uint256 a, uint256 b) internal constant returns (uint256) {
    uint256 c = a + b;
    assert(c >= a);
    return c;
  }
}


/**
 * @title ERC20Basic
 * @dev Simpler version of ERC20 interface
 * @dev see https://github.com/ethereum/EIPs/issues/179
 */
contract ERC20Basic {
  uint256 public totalSupply;
  function balanceOf(address who) constant returns (uint256);
  function transfer(address to, uint256 value) returns (bool);
  event Transfer(address indexed from, address indexed to, uint256 value);
}


/**
 * @title ERC20 interface
 * @dev see https://github.com/ethereum/EIPs/issues/20
 */
contract ERC20 is ERC20Basic {
  function allowance(address owner, address spender) constant returns (uint256);
  function transferFrom(address from, address to, uint256 value) returns (bool);
  function approve(address spender, uint256 value) returns (bool);
  event Approval(address indexed owner, address indexed spender, uint256 value);
}


/**
 * @title Basic token
 * @dev Basic version of StandardToken, with no allowances. 
 */
contract BasicToken is ERC20Basic {
  using SafeMath for uint256;

  mapping(address => uint256) balances;

  /**
  * @dev transfer token for a specified address
  * @param _to The address to transfer to.
  * @param _value The amount to be transferred.
  */
  function transfer(address _to, uint256 _value) returns (bool) {
    balances[msg.sender] = balances[msg.sender].sub(_value);
    balances[_to] = balances[_to].add(_value);
    Transfer(msg.sender, _to, _value);
    return true;
  }

  /**
  * @dev Gets the balance of the specified address.
  * @param _owner The address to query the the balance of. 
  * @return An uint256 representing the amount owned by the passed address.
  */
  function balanceOf(address _owner) constant returns (uint256 balance) {
    return balances[_owner];
  }
}


/**
 * @title Standard ERC20 token
 *
 * @dev Implementation of the basic standard token.
 * @dev https://github.com/ethereum/EIPs/issues/20
 * @dev Based on code by FirstBlood: https://github.com/Firstbloodio/token/blob/master/smart_contract/FirstBloodToken.sol
 */
contract StandardToken is ERC20, BasicToken {

  mapping (address => mapping (address => uint256)) allowed;


  /**
   * @dev Transfer tokens from one address to another
   * @param _from address The address which you want to send tokens from
   * @param _to address The address which you want to transfer to
   * @param _value uint256 the amout of tokens to be transfered
   */
  function transferFrom(address _from, address _to, uint256 _value) returns (bool) {
    var _allowance = allowed[_from][msg.sender];

    // Check is not needed because sub(_allowance, _value) will already throw if this condition is not met
    // require (_value <= _allowance);

    balances[_to] = balances[_to].add(_value);
    balances[_from] = balances[_from].sub(_value);
    allowed[_from][msg.sender] = _allowance.sub(_value);
    Transfer(_from, _to, _value);
    return true;
  }

  /**
   * @dev Aprove the passed address to spend the specified amount of tokens on behalf of msg.sender.
   * @param _spender The address which will spend the funds.
   * @param _value The amount of tokens to be spent.
   */
  function approve(address _spender, uint256 _value) returns (bool) {

    // To change the approve amount you first have to reduce the addresses`
    //  allowance to zero by calling `approve(_spender, 0)` if it is not
    //  already 0 to mitigate the race condition described here:
    //  https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
    require((_value == 0) || (allowed[msg.sender][_spender] == 0));

    allowed[msg.sender][_spender] = _value;
    Approval(msg.sender, _spender, _value);
    return true;
  }

  /**
   * @dev Function to check the amount of tokens that an owner allowed to a spender.
   * @param _owner address The address which owns the funds.
   * @param _spender address The address which will spend the funds.
   * @return A uint256 specifing the amount of tokens still avaible for the spender.
   */
  function allowance(address _owner, address _spender) constant returns (uint256 remaining) {
    return allowed[_owner][_spender];
  }
}


/**
 * @title Ownable
 * @dev The Ownable contract has an owner address, and provides basic authorization control
 * functions, this simplifies the implementation of "user permissions".
 */
contract Ownable {
  address public owner;


  /**
   * @dev The Ownable constructor sets the original `owner` of the contract to the sender
   * account.
   */
  function Ownable() {
    owner = msg.sender;
  }


  /**
   * @dev Throws if called by any account other than the owner.
   */
  modifier onlyOwner() {
    require(msg.sender == owner);
    _;
  }


  /**
   * @dev Allows the current owner to transfer control of the contract to a newOwner.
   * @param newOwner The address to transfer ownership to.
   */
  function transferOwnership(address newOwner) onlyOwner {
    if (newOwner != address(0)) {
      owner = newOwner;
    }
  }
}



/**
 * @title Pausable
 * @dev Base contract which allows children to implement an emergency stop mechanism.
 */
contract Pausable is Ownable {
  event Pause();
  event Unpause();

  bool public paused = false;


  /**
   * @dev modifier to allow actions only when the contract IS paused
   */
  modifier whenNotPaused() {
    require(!paused);
    _;
  }

  /**
   * @dev modifier to allow actions only when the contract IS NOT paused
   */
  modifier whenPaused {
    require(paused);
    _;
  }

  /**
   * @dev called by the owner to pause, triggers stopped state
   */
  function pause() onlyOwner whenNotPaused returns (bool) {
    paused = true;
    Pause();
    return true;
  }

  /**
   * @dev called by the owner to unpause, returns to normal state
   */
  function unpause() onlyOwner whenPaused returns (bool) {
    paused = false;
    Unpause();
    return true;
  }
}


/**
 * Pausable token
 *
 * Simple ERC20 Token example, with pausable token creation
 **/

contract PausableToken is StandardToken, Pausable {

  function transfer(address _to, uint _value) whenNotPaused returns (bool) {
    return super.transfer(_to, _value);
  }

  function transferFrom(address _from, address _to, uint _value) whenNotPaused returns (bool) {
    return super.transferFrom(_from, _to, _value);
  }
}



contract UNLB is PausableToken {

  string public constant name = "UnolaboToken";
  string public constant symbol = "UNLB";
  uint256 public constant decimals = 18;

  function UNLB() {
    owner = msg.sender;
  }

  function mint(address _x, uint _v) public onlyOwner {
    balances[_x] += _v;
    totalSupply += _v;
    Transfer(0x0, _x, _v);
  }
}



contract ICO is Pausable {

  uint public constant ICO_START_DATE = /*2017-11-27 17:00:00+8*/ 1511773200;
  uint public constant ICO_END_DATE   = /*2018-04-30 00:17:00+8*/ 1525018620;

  address public constant admin      = 0xFeC0714C2eE71a486B679d4A3539FA875715e7d8;
  address public constant teamWallet = 0xf16d5733A31D54e828460AFbf7D60aA803a61C51;

  UNLB public unlb;
  bool public isFinished = false;

  event ForeignBuy(address investor, uint unlbValue, string txHash);
  
  function ICO() {
    owner = admin;
    unlb = new UNLB();
    unlb.pause();
  }

  function pricePerWei() public constant returns(uint) {
    if     (now < /*2017-11-28 00:17:00+8*/ 1511799420) return 800.0 * 1 ether;
    else if(now < /*2017-11-29 00:17:00+8*/ 1511885820) return 750.0 * 1 ether;
    else if(now < /*2017-12-14 00:17:00+8*/ 1513181820) return 675.0 * 1 ether;
    else if(now < /*2018-01-10 00:17:00+8*/ 1515514620) return 575.0 * 1 ether;
    else if(now < /*2018-01-18 00:17:00+8*/ 1516205820) return 537.5 * 1 ether;
    else                                                return 500.0 * 1 ether;
  }


  function() public payable {
    require(!paused && now >= ICO_START_DATE && now < ICO_END_DATE);
    uint _tokenVal = (msg.value * pricePerWei()) / 1 ether;
    unlb.mint(msg.sender, _tokenVal);
  }

  function foreignBuy(address _investor, uint _unlbValue, string _txHash) external onlyOwner {
    require(!paused && now >= ICO_START_DATE && now < ICO_END_DATE);
    require(_unlbValue > 0);
    unlb.mint(_investor, _unlbValue);
    ForeignBuy(_investor, _unlbValue, _txHash);
  }
  
  function finish(address _team, address _fund, address _bounty, address _backers) external onlyOwner {
    require(now >= ICO_END_DATE && !isFinished);
    unlb.unpause();
    isFinished = true;

    uint _total = unlb.totalSupply() * 100 / (100 - 12 - 15 - 5 - 3);
    unlb.mint(_team,   (_total * 12) / 100);
    unlb.mint(_fund,   (_total * 15) / 100);
    unlb.mint(_bounty, (_total *  5) / 100);
    unlb.mint(_backers, (_total *  3) / 100);
  }


  function withdraw() external onlyOwner {
    teamWallet.transfer(this.balance);
  }
}